apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    defaultConfig {
        applicationId "com.dalpak.bringit"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 28
        versionName "1.14"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    useLibrary 'org.apache.http.legacy'
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [appName: "מטבח"]
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
            manifestPlaceholders = [appName: "Kitchen.Debug"]
        }

        debugTest {
            initWith debug
            applicationIdSuffix ".test"
            manifestPlaceholders = [appName: "Kitchen.Test"]
        }

        debugStage {
            initWith debug
            applicationIdSuffix ".stage"
            manifestPlaceholders = [appName: "Kitchen.Stage"]
        }

        debugLive {
            initWith debug
            applicationIdSuffix ".debugLive"
            manifestPlaceholders = [appName: "Kitchen.DebugLive"]
        }

        localHost {
            initWith debug
            applicationIdSuffix ".localhost"
            manifestPlaceholders = [appName: "Kitchen.LocalHost"]
        }
    }

    flavorDimensions 'default'
    productFlavors {
        kitchen {
            dimension 'default'
            versionName "28.1.14.12" //prod
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def builtType = variant.buildType.name
            def versionName = variant.versionName
            def flavor = variant.flavorName
            outputFileName = "${flavor}-${builtType}-${versionName}.apk"
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.recyclerview:recyclerview:1.2.0"
    implementation "androidx.cardview:cardview:1.0.0"

    implementation platform('com.google.firebase:firebase-bom:28.0.1')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    implementation 'com.android.volley:volley:1.2.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'com.github.woxthebox:draglistview:1.7.2'


    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'


}